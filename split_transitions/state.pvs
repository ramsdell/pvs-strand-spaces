% TPM model

% A model of a TPM with only one PCR and its values are messages.  The
% state of the PCR is either the boot state or the result of extending
% the current state.  The state is reflected into the algebra by
% associating a state with a message.  The proofs use the fact that
% the encoding function is injective.

% Note: this theory uses tg(0) for the initial value of a PCR so that
% constant should not be used for other purposes.

% John D. Ramsdell -- The MITRE Corporation -- January 2013

% Copyright (C) 2014 The MITRE Corporation.
% All rights reserved.
% See ../license.txt for terms.

state: theory
begin

  strand_spaces: library = "../strand_spaces"
  importing strand_spaces@init_alg

  % The state of the PCR is either the boot state or the result of
  % extending the PCR.
  state: datatype
  begin
    boot: boot?
    extend(m: mesg, st: state): extend?
  end state

  % The transition relation.  It is reflexive so as to allow
  % transitions that are used for state observation.
  trans(st, nx: state): bool =
    st = nx or
    cases nx of
      boot: true,
      extend(m, st1): st = st1
    endcases

  % State encoding

  % Encode the initial PCR value with this tag.
  initial_pcr_value: mesg = tg(0)
  % Encode extended PCR values with a hash.
  pcr(st: state): recursive mesg =
    cases st of
      boot: initial_pcr_value,
      extend(m, st): hs(pr(m, pcr(st)))
    endcases
    measure st by <<

  pcr_injective: lemma
  forall(st0, st1: state):
    pcr(st0) = pcr(st1) iff st0 = st1

  auto_rewrite+ pcr_injective

  transitions: library = "../transitions"
  importing transitions@paths[state, boot?, trans]

  % Used to show a PCR extension differs from a PCR observation.
  no_circularity_in_extend: lemma
    forall(m: mesg, st: state):
      st /= extend(m, st)

  % Facts about paths through the TPM transition relation.

  init_extend: lemma
    forall(p: path, x: mesg, st: state, k: nat):
      p(k) = extend(x, st) implies
        exists(j: nat):
          j < k and p(j) = st and p(j+1) = extend(x, st)

  prefix_extend: lemma
    forall(p: path, x: mesg, st: state, i, k: nat):
      i <= k and p(k) = extend(x, st) implies
        subterm(p(i), p(k)) or
        exists(j: nat):
          i <= j and j < k and p(j) = st and p(j+1) = extend(x, st)

  % If a state is not extended from a previous state, there must have
  % been a intervening boot.
  prefix_boot: lemma
    forall(p: path, i, k: nat):
      i <= k implies
        subterm(p(i), p(k)) or
        exists(j: nat): i <= j and j <= k and boot?(p(j))

  has(st: state, x: mesg): recursive bool =
    cases st of
      boot: false,
      extend(m, st): m = x or has(st, x)
    endcases
    measure st by <<

  % If a state is extended from one involving a message, and a
  % previous state is a boot state, there must have been an
  % intervening transition that extends with the message.
  boot_extend: lemma
    forall(p: path, x: mesg, i, k: nat):
      i < k and boot?(p(i)) and has(p(k), x) implies
        exists(j: nat):
          i <= j and j < k and p(j+1) = extend(x, p(j))

  % This is the composition of the above two lemmas.

  % If a state is extended from one involving a message and is not
  % extended from a previous state, there must have been an
  % intervening transition that extends with the message.
  prefix_boot_extend: lemma
    forall(p: path, x: mesg, i, k: nat):
      i <= k and has(p(k), x) implies
        subterm(p(i), p(k)) or
        exists(j: nat):
          i <= j and j < k and p(j+1) = extend(x, p(j))

end state
