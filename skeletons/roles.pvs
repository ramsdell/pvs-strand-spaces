% Roles as templates

% John D. Ramsdell -- The MITRE Corporation -- October 2014

% Copyright (C) 2014 The MITRE Corporation.
% All rights reserved.
% See ../license.txt for terms.

roles: theory
begin

  importing bundles

  % Assumptions are used to associate a set of messages with each
  % position in a trace.  The purpose of assumptions is defined below
  % in item_instance?.

  assumptions: type = nonempty_finite_sequence[setof[atom]]

  % Ensure all variables of sort message are acquired.  See the last
  % theorem in this theory for its purpose.
  all_acquired?(c: trace): bool =
    forall(x: (param?(c))):
      mvar?(x) implies
        exists(i: index(c)):
          acquired_index?(c, x, i)

  role_trace: type = (all_acquired?)

  pre_role: type = [# trace:   role_trace,
                      rnon:    {c: assumptions |  % Non-origination
                                length(c) = length(trace)},
                      runique: {c: assumptions |  % Unique origination
                                length(c) = length(trace)} #]

  % Variables in a non occur in the prefix
  all_non_vars_occur?(r: pre_role): bool =
    forall(i: index(r`rnon)):
      forall(x: (r`rnon(i))):
        forall(y: vari):
          occurs?(y, x) implies
            exists(j: below(i+1)):
              occurs?(y, t(r`trace(j)))

  % Uniques originate at point in trace
  all_uniq_orig?(r: pre_role): bool =
    forall(i: index(r`runique)):
      forall(x: (r`runique(i))):
        originates_index?(r`trace, x, i)

  role?(r: pre_role): bool =
    all_non_vars_occur?(r) and
    all_uniq_orig?(r)

  role: type = (role?)

  % Handle special case in which there are no role origination assumptions

  no_assumptions(n: posnat): recursive assumptions =
    if n = 1 then
      singleton_seq(emptyset)
    else
      add(emptyset, no_assumptions(n - 1))
    endif
    measure n

  no_assumptions_length: lemma
    forall(n: posnat): length(no_assumptions(n)) = n

  auto_rewrite+ no_assumptions_length

  no_assumptions?(c: assumptions): bool =
    forall(i: index(c)): empty?(c(i))

  has_no_assumptions: lemma
    forall(n: posnat): no_assumptions?(no_assumptions(n))

  has_empty_assumptions: lemma
    forall(n: posnat, i: below[n]):
      empty?(no_assumptions(n)(i))

  no_rnon_assumptions: lemma
    forall(r: pre_role):
      r`rnon = no_assumptions(length(r`trace)) implies
        all_non_vars_occur?(r)

  no_runique_assumptions: lemma
    forall(r: pre_role):
      r`runique = no_assumptions(length(r`trace)) implies
        all_uniq_orig?(r)

  % A simple role item has only a trace
  simple_role(c: role_trace): role =
    (# trace   := c,
       rnon    := no_assumptions(length(c)),
       runique := no_assumptions(length(c)) #)

  % Is strand z in strand space ss an instance of role r?
  inst(ss: strand_space)(z: strand(ss), r: role): bool =
    let c = ss(z) in
    length(c) <= length(r`trace) and
    cases instance(r`trace, c) of
      bottom: false,
      up(s):
        let h = length(c) in
        (forall(j: below(h)):
          forall(m: (r`rnon(j))):
            non(ss)(subst(m, s))) and
        (forall(j: below(h)):
          forall(m: (r`runique(j))):
            uniq(ss)(subst(m, s), (z, j)))
     endcases

  simple_inst: lemma
    forall(ss: strand_space, z: strand(ss), c: role_trace):
      inst(ss)(z, simple_role(c)) iff
        length(ss(z)) <= length(c) and
        up?(instance(c, ss(z)))

  auto_rewrite+ simple_inst

  % Node prespective

  % Is node n in strand space ss an instance of role r?
  ninst(ss: strand_space)(n: node(ss), r: role): bool =
    let c = ss(n`1) ^ (0, n`2) in
    length(c) <= length(r`trace) and
    cases instance(r`trace, c) of
      bottom: false,
      up(s):
        let h = length(c) in
        (forall(j: below(h)):
          forall(m: (r`rnon(j))):
            non(ss)(subst(m, s))) and
        (forall(j: below(h)):
          forall(m: (r`runique(j))):
            uniq(ss)(subst(m, s), (n`1, j)))
     endcases

  simple_ninst: lemma
    forall(ss: strand_space, n: node(ss), c: role_trace):
      ninst(ss)(n, simple_role(c)) iff
        n`2 < length(c) and
        up?(instance(c, ss(n`1) ^ (0, n`2)))

  auto_rewrite+ simple_ninst

  % Role assignments

  pre_role_assignment(b: bundle): type = [strand(b`space) -> role]

  % Function ra is a role assignment for bundle b if each trace is an
  % instance of the assigned role.
  role_assignment?(b: bundle)(ra: pre_role_assignment(b)): bool =
    forall(s: strand(b`space)):
      inst(b`space)(s, ra(s))

  role_assignment(b: bundle): type = (role_assignment?(b))

  % Protocols

  protocol: type = setof[role]

  % The range of the role assignment is the protocol
  protocol_assignment?(p: protocol)(b: bundle)(ra: role_assignment(b)): bool =
    forall(s: strand(b`space)): p(ra(s))

  protocol_assignment(p: protocol, b: bundle): type =
    (protocol_assignment?(p)(b))

  % Bundle b is a run of protocol p if there exists a protocol assignment
  run?(p: protocol)(b: bundle): bool =
    exists(pa: protocol_assignment(p, b)): true

  % The obvious lemma about bundles with protocol assigments and runs:
  bundles_with_assignments_are_runs: lemma
    forall(p: protocol, b: bundle, pa: protocol_assignment(p, b)):
      run?(p)(b)

  run(p: protocol): type = (run?(p))

  % A bundle that is a run of a protocol has no variables of sort
  % message.  This is the result and the purpose of the acquired
  % constraint in roles.
  no_message_vars: theorem
    forall(p: protocol, b: run(p), x: (svars(b`space))):
      not mvar?(x)

  % Role and parameter predicates for shape analysis sentences

  % Role predicate
  role_pred(p: protocol, r: (p), i: index(r`trace))
           (b: bundle)(n: node(b`space)): bool =
    n`2 = i and ninst(b`space)(n, r)

  % Parameter predicate
  param_pred(p: protocol, r: (p), v: (param?(r`trace)))
            (b: bundle)(n: node(b`space), t: mesg): bool =
    ninst(b`space)(n, r) and
    param?(r`trace ^ (0, n`2))(v) and
    lookup(v, down(instance(r`trace, b`space(n`1) ^ (0, n`2)))) = t

  % Theorems about role and parameter formulas

  role_role_index: lemma
    forall(p: protocol, r, s: (p), i: index(r`trace), j: index(s`trace),
           b: bundle, n: node(b`space)):
      role_pred(p, r, i)(b)(n) and role_pred(p, s, j)(b)(n) implies i = j

  role_param_occurs: lemma
    forall(p: protocol, r: (p), i: index(r`trace),
           v: (param?(r`trace)), t: mesg,
           b: bundle, n: node(b`space)):
      role_pred(p, r, i)(b)(n) and
      param_pred(p, r, v)(b)(n, t) implies
        param?(r`trace ^ (0, i))(v)

  role_param_role: lemma
    forall(p: protocol, r, s: (p), i: index(r`trace),
           v: (param?(s`trace)), t: mesg,
           b: bundle, n: node(b`space)):
      role_pred(p, r, i)(b)(n) and
      param_pred(p, s, v)(b)(n, t) implies
        i < length(s`trace) and role_pred(p, s, i)(b)(n)
        
  param_slide: lemma
    forall(p: protocol, r: (p), i, j: index(r`trace),
           v: (param?(r`trace)), t: mesg,
           b: bundle, n0, n1: node(b`space)):
      role_pred(p, r, i)(b)(n0) and
      param_pred(p, r, v)(b)(n0, t) and
      sprec?(b)(n0, n1) and
      role_pred(p, r, j)(b)(n1) implies
        param_pred(p, r, v)(b)(n1, t)

end roles
