% The adversary model for the message algebra defined in the init_alg
% theory.

% John D. Ramsdell -- The MITRE Corporation -- December 2012

% Copyright (C) 2014 The MITRE Corporation.
% All rights reserved.
% See ../license.txt for terms.

adversary: theory
begin

  strand_spaces: library = "../strand_spaces"
  importing strand_spaces@init_alg
  importing roles[mesg, carried_by?]

  % The five adversary roles

  % Create an atom

  % The trace
  create_trace(x: atom): trace =
    singleton_seq(send(x))

  % The role
  create_role(ri: role_item): bool =
    exists(x: atom): ri = simple_role_item(create_trace(x))

  % Penetrator non-origination forbids the use of the create role.
  % Hopefully a regular role does not start out like a create role.
  pen_non(ss: strand_space)(m: atom): bool =
    forall(s: strand(ss)): event_at(ss)(s, 0) /= send(m)

  % Create a tag

  tag_trace(x: tag): trace =
    singleton_seq(send(x))

  tag_role(ri: role_item): bool =
    exists(x: tag): ri = simple_role_item(tag_trace(x))

  % Hash a message

  hash_trace(x: mesg): trace =
    doubleton_seq(recv(x), send(hs(x)))

  hash_role(ri: role_item): bool =
    exists(x: mesg): ri = simple_role_item(hash_trace(x))

  % Pair two messages

  pair_trace(x, y: mesg): trace =
    tripleton_seq(recv(x), recv(y), send(pr(x, y)))

  pair_role(ri: role_item): bool =
    exists(x, y: mesg): ri = simple_role_item(pair_trace(x, y))

  % Separate two messages in a pair

  sep_trace(x, y: mesg): trace =
    tripleton_seq(recv(pr(x,y)), send(x), send(y))

  sep_role(ri: role_item): bool =
    exists(x, y: mesg): ri = simple_role_item(sep_trace(x, y))

  % Encrypt a message with a key

  enc_trace(x: mesg, k: key): trace =
    tripleton_seq(recv(x), recv(k), send(en(x, k)))

  enc_role(ri: role_item): bool =
    exists(x: mesg, k: key): ri = simple_role_item(enc_trace(x, k))

  % Decrypt a message with the inverse of a key

  dec_trace(x: mesg, k: key): trace =
    tripleton_seq(recv(en(x, k)), recv(invk(k)), send(x))

  dec_role(ri : role_item): bool =
    exists(x: mesg, k: key): ri = simple_role_item(dec_trace(x, k))

  % All the adversary roles as a finite set
  adversary_roles: protocol =
    add(create_role,
        add(tag_role,
            add(hash_role,
                add(pair_role,
                    add(sep_role,
                        add(enc_role,
                            singleton(dec_role)))))))

  enumerate_adversary_roles: lemma
  forall(r: role):
    adversary_roles(r) iff
      r = create_role or
      r = tag_role or
      r = hash_role or
      r = pair_role or
      r = sep_role or
      r = enc_role or
      r = dec_role

  adversary_strands_are_short: lemma
    forall(b: bundle, s: strand(b`space), h: nat, r: (adversary_roles)):
      h >= 4 implies not role_pred(b)(s, h, r)

end adversary
